mod NARROWING-VENDING-MACHINE is
 sorts Coin Item Marking Money State .
 subsort Coin < Money .

 op empty : -> Money .
 op __ : Money Money -> Money [assoc comm id: empty] .

 subsort Money Item < Marking .
 op __ : Marking Marking -> Marking [assoc comm id: empty] .

 op <_> : Marking -> State .
 ops $ q : -> Coin .
 ops a c : -> Item .

 var M : Marking .

 rl [narrow] : < M $ > => < M c > [narrowing] .
 rl [narrow] : < M $ > => < M a q > [narrowing] .
 eq [change] : q q q q = $ [variant] .
endm

narrow < C1:Coin C2:Coin C3:Coin > =>* < M:Money a c > .
vu-narrow < C1:Coin C2:Coin C3:Coin > =>* < M:Money a c > .
